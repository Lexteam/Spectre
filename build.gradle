plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.1.2'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}
defaultTasks 'clean', 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'xyz.lexteam'
version '1.0.0-SNAPSHOT'
description = "Spectre - The Lexteam module loader."
ext.url = 'https://www.lexteam.xyz/'

repositories {
    mavenCentral()
}

dependencies {
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    newLine = false
}

jar {
    classifier = 'base'

    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version
        )
    }
}

shadowJar {
    classifier = ''
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives shadowJar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getenv('MAVEN_RELEASES') != null) {
                repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.archivesBaseName
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = "https://github.com/Lexteam/Spectre"
                        connection = "scm:git:https://github.com/Lexteam/Spectre.git"
                        developerConnection = "scm:git:git@github.com:Lexteam/Spectre.git"
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/Lexteam/Spectre/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
